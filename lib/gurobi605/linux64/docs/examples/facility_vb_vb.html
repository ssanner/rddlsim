<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">

<!--Converted with LaTeX2HTML 2002-2-1 (1.71)
original version by:  Nikos Drakos, CBLU, University of Leeds
* revised and updated by:  Marcus Hennecke, Ross Moore, Herb Swan
* with significant contributions from:
  Jens Lippmann, Marek Rouchal, Martin Wilck and others -->
<HTML>
<HEAD>
<TITLE>facility_vb.vb</TITLE>
<META NAME="description" CONTENT="facility_vb.vb">
<META NAME="keywords" CONTENT="examples">
<META NAME="resource-type" CONTENT="document">
<META NAME="distribution" CONTENT="global">

<META NAME="Generator" CONTENT="LaTeX2HTML v2002-2-1">
<META HTTP-EQUIV="Content-Style-Type" CONTENT="text/css">

<LINK REL="STYLESHEET" HREF="examples.css">

<LINK REL="next" HREF="feasopt_vb_vb.html">
<LINK REL="previous" HREF="diet_vb_vb.html">
<LINK REL="up" HREF="visual_basic_examples.html">
<LINK REL="next" HREF="feasopt_vb_vb.html">
</HEAD>

<BODY >

<DIV CLASS="navigation"><!--Navigation Panel-->
<A NAME="tex2html1651"
  HREF="feasopt_vb_vb.html">
<IMG WIDTH="37" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="next" SRC="next.png"></A> 
<A NAME="tex2html1649"
  HREF="visual_basic_examples.html">
<IMG WIDTH="26" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="up" SRC="up.png"></A> 
<A NAME="tex2html1643"
  HREF="diet_vb_vb.html">
<IMG WIDTH="63" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="previous" SRC="prev.png"></A>   
<BR>
<B> Next:</B> <A NAME="tex2html1652"
  HREF="feasopt_vb_vb.html">feasopt_vb.vb</A>
<B> Up:</B> <A NAME="tex2html1650"
  HREF="visual_basic_examples.html">Visual Basic Examples</A>
<B> Previous:</B> <A NAME="tex2html1644"
  HREF="diet_vb_vb.html">diet_vb.vb</A>
<BR>
<BR></DIV>
<!--End of Navigation Panel-->

<H3><A NAME="SECTION00025400000000000000"></A>
<A NAME="subsubsection:facility_vb.vb"></A>
<BR>
facility_vb.vb
</H3>

<P>
<BR>
<PRE  CLASS="verbatim">' Copyright 2014, Gurobi Optimization, Inc.
'
' Facility location: a company currently ships its product from 5 plants
' to 4 warehouses. It is considering closing some plants to reduce
' costs. What plant(s) should the company close, in order to minimize
' transportation and fixed costs?
'
' Based on an example from Frontline Systems:
' http://www.solver.com/disfacility.htm
' Used with permission.

Imports System
Imports Gurobi

Class facility_vb
    Shared Sub Main()
        Try

            ' Warehouse demand in thousands of units
            Dim Demand As Double() = New Double() {15, 18, 14, 20}

            ' Plant capacity in thousands of units
            Dim Capacity As Double() = New Double() {20, 22, 17, 19, 18}

            ' Fixed costs for each plant
            Dim FixedCosts As Double() = New Double() {12000, 15000, 17000, 13000, _
                                                       16000}

            ' Transportation costs per thousand units
            Dim TransCosts As Double(,) = New Double(,) {{4000, 2000, 3000, 2500, 4500}, _
                                                         {2500, 2600, 3400, 3000, 4000}, _
                                                         {1200, 1800, 2600, 4100, 3000}, _
                                                         {2200, 2600, 3100, 3700, 3200}}

            ' Number of plants and warehouses
            Dim nPlants As Integer = Capacity.Length
            Dim nWarehouses As Integer = Demand.Length

            ' Model
            Dim env As New GRBEnv()
            Dim model As New GRBModel(env)
            model.Set(GRB.StringAttr.ModelName, "facility")

            ' Plant open decision variables: open(p) == 1 if plant p is open.
            Dim open As GRBVar() = New GRBVar(nPlants - 1) {}
            For p As Integer = 0 To nPlants - 1
                open(p) = model.AddVar(0, 1, FixedCosts(p), GRB.BINARY, "Open" &amp; p)
            Next

            ' Transportation decision variables: how much to transport from
            ' a plant p to a warehouse w
            Dim transport As GRBVar(,) = New GRBVar(nWarehouses - 1, nPlants - 1) {}
            For w As Integer = 0 To nWarehouses - 1
                For p As Integer = 0 To nPlants - 1
                    transport(w, p) = model.AddVar(0, GRB.INFINITY, _
                                                   TransCosts(w, p), GRB.CONTINUOUS, _
                                                   "Trans" &amp; p &amp; "." &amp; w)
                Next
            Next

            ' The objective is to minimize the total fixed and variable costs
            model.Set(GRB.IntAttr.ModelSense, 1)

            ' Update model to integrate new variables
            model.Update()

            ' Production constraints
            ' Note that the right-hand limit sets the production to zero if
            ' the plant is closed
            For p As Integer = 0 To nPlants - 1
                Dim ptot As GRBLinExpr = 0
                For w As Integer = 0 To nWarehouses - 1
                    ptot.AddTerm(1.0, transport(w, p))
                Next
                model.AddConstr(ptot &lt;= Capacity(p) * open(p), "Capacity" &amp; p)
            Next

            ' Demand constraints
            For w As Integer = 0 To nWarehouses - 1
                Dim dtot As GRBLinExpr = 0
                For p As Integer = 0 To nPlants - 1
                    dtot.AddTerm(1.0, transport(w, p))
                Next
                model.AddConstr(dtot = Demand(w), "Demand" &amp; w)
            Next

            ' Guess at the starting point: close the plant with the highest
            ' fixed costs; open all others

            ' First, open all plants
            For p As Integer = 0 To nPlants - 1
                open(p).Set(GRB.DoubleAttr.Start, 1.0)
            Next

            ' Now close the plant with the highest fixed cost
            Console.WriteLine("Initial guess:")
            Dim maxFixed As Double = -GRB.INFINITY
            For p As Integer = 0 To nPlants - 1
                If FixedCosts(p) &gt; maxFixed Then
                    maxFixed = FixedCosts(p)
                End If
            Next
            For p As Integer = 0 To nPlants - 1
                If FixedCosts(p) = maxFixed Then
                    open(p).Set(GRB.DoubleAttr.Start, 0.0)
                    Console.WriteLine("Closing plant " &amp; p &amp; vbLf)
                    Exit For
                End If
            Next

            ' Use barrier to solve root relaxation
            model.GetEnv().Set(GRB.IntParam.Method, GRB.METHOD_BARRIER)

            ' Solve
            model.Optimize()

            ' Print solution
            Console.WriteLine(vbLf &amp; "TOTAL COSTS: " &amp; model.Get(GRB.DoubleAttr.ObjVal))
            Console.WriteLine("SOLUTION:")
            For p As Integer = 0 To nPlants - 1
                If open(p).Get(GRB.DoubleAttr.X) = 1.0 Then
                    Console.WriteLine("Plant " &amp; p &amp; " open:")
                    For w As Integer = 0 To nWarehouses - 1
                        If transport(w, p).Get(GRB.DoubleAttr.X) &gt; 0.0001 Then
                            Console.WriteLine("  Transport " &amp; _
                                              transport(w, p).Get(GRB.DoubleAttr.X) &amp; _
                                              " units to warehouse " &amp; w)
                        End If
                    Next
                Else
                    Console.WriteLine("Plant " &amp; p &amp; " closed!")
                End If

            Next

            ' Dispose of model and env
            model.Dispose()
            env.Dispose()

        Catch e As GRBException
            Console.WriteLine("Error code: " &amp; e.ErrorCode &amp; ". " &amp; e.Message)
        End Try
    End Sub
End Class
</PRE>
<P>

<DIV CLASS="navigation"><HR>
<!--Navigation Panel-->
<A NAME="tex2html1651"
  HREF="feasopt_vb_vb.html">
<IMG WIDTH="37" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="next" SRC="next.png"></A> 
<A NAME="tex2html1649"
  HREF="visual_basic_examples.html">
<IMG WIDTH="26" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="up" SRC="up.png"></A> 
<A NAME="tex2html1643"
  HREF="diet_vb_vb.html">
<IMG WIDTH="63" HEIGHT="24" ALIGN="BOTTOM" BORDER="0" ALT="previous" SRC="prev.png"></A>   
<BR>
<B> Next:</B> <A NAME="tex2html1652"
  HREF="feasopt_vb_vb.html">feasopt_vb.vb</A>
<B> Up:</B> <A NAME="tex2html1650"
  HREF="visual_basic_examples.html">Visual Basic Examples</A>
<B> Previous:</B> <A NAME="tex2html1644"
  HREF="diet_vb_vb.html">diet_vb.vb</A></DIV>
<!--End of Navigation Panel-->

</BODY>
</HTML>
